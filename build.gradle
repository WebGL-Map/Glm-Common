plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.jfrog.artifactory" version "4.4.15"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

group 'net.reallifegames'
version '1.0.7'
archivesBaseName = 'GlmCommon'

project.ext.rlgMavenUser = hasProperty('rlgMavenUser') ? rlgMavenUser : System.getenv('rlgMavenUser')
project.ext.rlgMavenPassword = hasProperty('rlgMavenPassword') ? rlgMavenPassword : System.getenv('rlgMavenPassword')

repositories {
    mavenCentral()

    maven {
        name = 'reallifegames'
        url = 'https://reallifegames.net/artifactory/gradle-release-local'
    }
}

dependencies {
    compile 'net.reallifegames:GlmApi:1.0.3'
}

license {
    header project.file("LICENSE.txt")
    strictCheck true
    mapping {
        java='SLASHSTAR_STYLE'
    }
}

jar {
    baseName = archivesBaseName
    version =  version
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifactory {
    contextUrl = 'https://reallifegames.net/artifactory/'
    publish {
        repository {
            repoKey = 'gradle-release-local'
            username = "${rlgMavenUser}"
            password = "${rlgMavenPassword}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "${group}"
            artifactId "${archivesBaseName}"
            version "${version}"
            from components.java

            artifact (sourcesJar) {
                classifier "sources"
            }
            artifact (javadocJar) {
                classifier "javadoc"
            }
        }
    }
}